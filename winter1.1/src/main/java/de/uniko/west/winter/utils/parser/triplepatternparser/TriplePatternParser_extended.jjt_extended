// SPARQL Pattern Syntax inspired by the W3C SPARQL definition found on
// http://www.w3.org/TR/rdf-sparql-query/
// Token and Grammar insprirend by the Sesame sparql.jj written by Arjohn Kampman
// Copyright Aduna (http://www.aduna-software.com/) (c) 1997-2007.
// Licensed under the Aduna BSD-style license

// Author:  Stefan Scheglmann schegi@uni-koblenz.de

options
{
}

PARSER_BEGIN(SPARQLPatternParser)

package de.west.winter.utils.triplepatternparser_extended ;

import java.io.StringReader;
import java.net.URI;
import de.west.winter.utils.RDF;
import de.west.winter.utils.XMLSchema;

public class SPARQLPatternParser
{
	public static ASTQueryContainer parsePattern (String pattern) throws TokenMgrError, ParseException	{
	  	SPARQLPatternParser spp = new SPARQLPatternParser(new StringReader(pattern));
	  	return spp.QueryContainer();	}

	private static String _trimString(String s, int n) {
	  if (s.length() >= 2) {
	    s = s.substring(n, s.length() - n);
	  }
	  return s;
	}
}

PARSER_END(SPARQLPatternParser)

ASTQueryContainer QueryContainer():
{}
{
    TriplesBlock() <EOF>
    { return jjtThis; }
}


// Tokens

TOKEN :
{
    // Whitespace characters
    <#WS_CHAR: " " | "\t" | "\n" | "\r" | "\f">
}

SKIP :
{
    <WHITESPACE: <WS_CHAR>>
}

SPECIAL_TOKEN :
{
    <SINGLE_LINE_COMMENT: "#" (~["\n","\r"])*>
}

// Special characters
TOKEN :
{
    <LPAREN: "(">
|   <RPAREN: ")">
|   <LBRACE: "{">
|   <RBRACE: "}">
|   <LBRACK: "[">
|   <RBRACK: "]">
|   <SEMICOLON: ";">
|   <COMMA: ",">
|   <DOT: ".">
|   <EQ: "=">
|   <NE: "!=">
|   <GT: ">">
|   <LT: "<">
|   <LE: "<=">
|   <GE: ">=">
|   <NOT: "!">
|   <OR: "||">
|   <AND: "&&">
|   <PLUS: "+">
|   <MINUS: "-">
|   <STAR: "*">
|   <SLASH: "/">
|   <DT_PREFIX: "^^">
|   <NIL: <LPAREN> (<WS_CHAR>)* <RPAREN>>
|   <ANON: <LBRACK> (<WS_CHAR>)* <RBRACK>>
}

TOKEN :
{
    <IS_A: "a"> // Note: case-sensitive!
}

TOKEN [IGNORE_CASE] :
{
    <BASE: "base">
|   <PREFIX: "prefix">
|   <SELECT: "select">
|   <CONSTRUCT: "construct">
|   <DESCRIBE: "describe">
|   <ASK: "ask">
|   <DISTINCT: "distinct">
|   <REDUCED: "reduced">
|   <FROM: "from">
|   <NAMED: "named">
|   <WHERE: "where">
|   <ORDER: "order">
|   <BY: "by">
|   <ASC: "asc">
|   <DESC: "desc">
|   <LIMIT: "limit">
|   <OFFSET: "offset">
|   <OPTIONAL: "optional">
|   <GRAPH: "graph">
|   <UNION: "union">
|   <FILTER: "filter">
|   <STR: "str">
|   <LANG: "lang">
|   <LANGMATCHES: "langmatches">
|   <DATATYPE: "datatype">
|   <BOUND: "bound">
|   <SAMETERM: "sameTerm">
|   <IS_IRI: ("isIRI"|"isURI")>
|   <IS_BLANK: "isBlank">
|   <IS_LITERAL: "isLiteral">
|   <REGEX: "regex">
|   <TRUE: "true">
|   <FALSE: "false">
}

TOKEN:
{
    <Q_IRI_REF: "<" (~["<", ">", "\"", "{", "}", "|", "^", "`", "\\", "\u0000"-"\u0020"])* ">">

|   <PNAME_NS: (<PN_PREFIX>)? ":">
|   <PNAME_LN: (<PN_PREFIX>)? ":" <PN_LOCAL>> // Note: PN_LOCAL not optional, case handled by PNAME_NS

|   <BLANK_NODE_LABEL: "_:" <PN_LOCAL>>

|   <VAR1: "?" <VARNAME>>
|   <VAR2: "$" <VARNAME>>
|   <LANGTAG: "@" (<ALPHA>)+ ("-" (<ALPHA>|<NUM>)+)*>
|   <INTEGER: (<NUM>)+>
|   <INTEGER_POSITIVE: "+" <INTEGER>>
|   <INTEGER_NEGATIVE: "-" <INTEGER>>
|   <DECIMAL: (<DECIMAL1>|<DECIMAL2>)>
|   <#DECIMAL1: (<NUM>)+ "." (<NUM>)*>
|   <#DECIMAL2: "." (<NUM>)+>
|   <DECIMAL_POSITIVE: "+" <DECIMAL>>
|   <DECIMAL_NEGATIVE: "-" <DECIMAL>>
|   <DOUBLE: (<DOUBLE1>|<DOUBLE2>|<DOUBLE3>)>
|   <#DOUBLE1: (<NUM>)+ "." (<NUM>)* <EXPONENT>>
|   <#DOUBLE2: "." (<NUM>)+ <EXPONENT>>
|   <#DOUBLE3: (<NUM>)+ <EXPONENT>>
|   <#EXPONENT: ["e","E"] (["+","-"])? (<NUM>)+>
|   <DOUBLE_POSITIVE: "+" <DOUBLE>>
|   <DOUBLE_NEGATIVE: "-" <DOUBLE>>
|   <STRING_LITERAL1: "'" (<SAFE_CHAR1>|<ECHAR>)* "'">
|   <STRING_LITERAL2: "\"" (<SAFE_CHAR2>|<ECHAR>)* "\"">
|   <STRING_LITERAL_LONG1: "'''" ( ("'"|"''")? (<SAFE_CHAR_LONG1>|<ECHAR>) )* "'''">
|   <STRING_LITERAL_LONG2: "\"\"\"" ( ("\""|"\"\"")? (<SAFE_CHAR_LONG2>|<ECHAR>) )* "\"\"\"">
|   <#SAFE_CHAR1: (~["'", "\\", "\r", "\n"])>
|   <#SAFE_CHAR2: (~["\"", "\\", "\r", "\n"])>
|   <#SAFE_CHAR_LONG1: (~["'","\\"])>
|   <#SAFE_CHAR_LONG2: (~["\"","\\"])>
|   <#ECHAR: "\\" ["t", "b", "n", "r", "f", "\\", "\"", "'"]>
|   <#HEX: ["0"-"9"] | ["A"-"F"] | ["a"-"f"]>
|   <#ALPHA: ["a"-"z","A"-"Z"]>
|   <#NUM: ["0"-"9"]>
}

TOKEN:
{
    <#PN_CHARS_BASE:
        <ALPHA> | ["\u00C0"-"\u00D6"] | ["\u00D8"-"\u00F6"] | ["\u00F8"-"\u02FF"] | ["\u0370"-"\u037D"]
      | ["\u037F"-"\u1FFF"] | ["\u200C"-"\u200D"] | ["\u2070"-"\u218F"] | ["\u2C00"-"\u2FEF"]
      | ["\u3001"-"\uD7FF"] | ["\uF900"-"\uFDCF"] | ["\uFDF0"-"\uFFFD"]
      // | ["\u10000"-"\uEFFFF"] FIXME: JavaCC/Java can't handle this?
    >
|   <#PN_CHARS_U: <PN_CHARS_BASE> | "_">
|   <#PN_START_CHAR: <PN_CHARS_U> | <NUM>>
|   <#VAR_CHAR: <PN_START_CHAR> | "\u00B7" | ["\u0300"-"\u036F"] | ["\u203F"-"\u2040"]>
|   <#PN_END_CHAR: <VAR_CHAR> | "-">
|   <#PN_CHAR: <PN_END_CHAR> | ".">

|   <#PN_PREFIX: <PN_CHARS_BASE> ( (<PN_CHAR>)* <PN_END_CHAR> )?>
|   <#PN_LOCAL: <PN_START_CHAR> ( (<PN_CHAR>)* <PN_END_CHAR> )?>
|   <#VARNAME: <PN_START_CHAR> (<VAR_CHAR>)*>
}

void TriplesBlock() #void :
{}
{
    // Note: recursive rule rewriten to non-recursive rule, requires lookahead
    TriplesSameSubject() ( LOOKAHEAD(2) <DOT> TriplesSameSubject() )* [<DOT>]
}

void TriplesSameSubject() :
{}
{
    VarOrTerm() PropertyList()
|
    TriplesNode() [PropertyList()]
}

void VarOrTerm() #void :
{}
{
    Var()
|
    GraphTerm()
}

void PropertyList() :
{}
{
    Verb() ObjectList() [ <SEMICOLON> [PropertyList()] ]
}

void TriplesNode() #void :
{}
{
    Collection()
|
    BlankNodePropertyList()
}

void Var() :
{ Token t; }
{
    ( t = <VAR1> | t = <VAR2> )
    { jjtThis.setName(t.image.substring(1)); }
}

void GraphTerm() #void :
{
    boolean positive = true;
}
{
    IRIref()
|
    RDFLiteral()
|
    NumericLiteral()
|
    BooleanLiteral()
|
    BlankNode()
|
    <NIL> {jjtThis.setValue(RDF.NIL.toString());} #IRI
}

void Verb() #void :
{}
{
    VarOrIRIref()
|
    <IS_A> { jjtThis.setValue(RDF.TYPE.toString()); } #IRI
}

void ObjectList():
{}
{
    Object() ( <COMMA> Object() )*
}

void Collection() :
{}
{
    <LPAREN> ( GraphNode() )+ <RPAREN>
}

void BlankNodePropertyList() :
{}
{
    <LBRACK> PropertyList() <RBRACK>
}

void VarOrIRIref() #void :
{}
{
    Var()
|
    IRIref()
}

void Object() #void :
{}
{
    GraphNode()
}

void GraphNode() #void :
{}
{
    VarOrTerm()
|
    TriplesNode()
}

void IRIref() #void :
{}
{
    IRI()
|
    PrefixedName()
}

void PrefixedName() #QName :
{ Token t; }
{
    (t = <PNAME_LN> | t = <PNAME_NS>)
    {jjtThis.setValue(t.image);}
}

void BlankNode() :
{ Token t; }
{
    t = <BLANK_NODE_LABEL> {jjtThis.setID(t.image.substring(2));}
|
    <ANON>
}

void IRI() :
{ Token t; }
{
    t = <Q_IRI_REF> {jjtThis.setValue(_trimString(t.image, 1));}
}

// Literals

void RDFLiteral() :
{ Token t; }
{
    String()
    [
        t = <LANGTAG> {jjtThis.setLang(t.image.substring(1));}
    |
        <DT_PREFIX> IRIref()
    ]
}

void NumericLiteral() #void :
{}
{
    (
        NumericLiteralUnsigned()
    |   NumericLiteralPositive()
    |   NumericLiteralNegative()
    )
}

void NumericLiteralUnsigned() #NumericLiteral :
{ Token t; URI datatype; }
{
    (
        t = <INTEGER> { datatype = XMLSchema.INTEGER; }
    |   t = <DECIMAL> { datatype = XMLSchema.DECIMAL; }
    |   t = <DOUBLE> { datatype = XMLSchema.DOUBLE; }
    )

    {
        jjtThis.setValue(t.image);
        jjtThis.setDatatype(datatype);
    }
}

void NumericLiteralPositive() #NumericLiteral :
{ Token t; URI datatype; }
{
    (
        t = <INTEGER_POSITIVE> { datatype = XMLSchema.INTEGER; }
    |   t = <DECIMAL_POSITIVE> { datatype = XMLSchema.DECIMAL; }
    |   t = <DOUBLE_POSITIVE> { datatype = XMLSchema.DOUBLE; }
    )

    {
        jjtThis.setValue(t.image);
        jjtThis.setDatatype(datatype);
    }
}

void NumericLiteralNegative() #NumericLiteral :
{ Token t; URI datatype; }
{
    (
        t = <INTEGER_NEGATIVE> { datatype = XMLSchema.INTEGER; }
    |   t = <DECIMAL_NEGATIVE> { datatype = XMLSchema.DECIMAL; }
    |   t = <DOUBLE_NEGATIVE> { datatype = XMLSchema.DOUBLE; }
    )

    {
        jjtThis.setValue(t.image);
        jjtThis.setDatatype(datatype);
    }
}

void BooleanLiteral() #void :
{}
{
    <TRUE> #True
|   <FALSE> #False
}

void String() :
{ Token t; }
{
    ( t = <STRING_LITERAL1> | t= <STRING_LITERAL2> )
    {jjtThis.setValue(_trimString(t.image, 1));}
|
    ( t = <STRING_LITERAL_LONG1> | t = <STRING_LITERAL_LONG2>)
    {jjtThis.setValue(_trimString(t.image, 3));}
}

// Checker

void Expression() #void :
{}
{
    BrackettedExpression()
|   BuiltInCall()
|   IRIref()
|   RDFLiteral()
|   NumericLiteral()
|   BooleanLiteral()
|   Var()
}

void BrackettedExpression() #void :
{}
{
    <LPAREN> Expression() <RPAREN>
}

void BuiltInCall() #void :
{}
{
    Str()
|   Lang()
|   LangMatches()
|   Datatype()
|   Bound()
|   SameTerm()
|   IsIRI()
|   IsBlank()
|   IsLiteral()
|   RegexExpression()
}

void Str() :
{}
{
    <STR> <LPAREN> Expression() <RPAREN>
}

void Lang() :
{}
{
    <LANG> <LPAREN> Expression() <RPAREN>
}

void LangMatches() :
{}
{
    <LANGMATCHES> <LPAREN> Expression() <COMMA> Expression() <RPAREN>
}

void Datatype() :
{}
{
    <DATATYPE> <LPAREN> Expression() <RPAREN>
}

void Bound() :
{}
{
    <BOUND> <LPAREN> Var() <RPAREN>
}

void SameTerm() :
{}
{
    <SAMETERM> <LPAREN> Expression() <COMMA> Expression() <RPAREN>
}

void IsIRI() :
{}
{
    <IS_IRI> <LPAREN> Expression() <RPAREN>
}

void IsBlank() :
{}
{
    <IS_BLANK> <LPAREN> Expression() <RPAREN>
}

void IsLiteral() :
{}
{
    <IS_LITERAL> <LPAREN> Expression() <RPAREN>
}

void RegexExpression() :
{}
{
    <REGEX> <LPAREN> Expression() <COMMA> Expression() [ <COMMA> Expression() ] <RPAREN>
}
